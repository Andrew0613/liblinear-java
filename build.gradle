buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'java'
    id 'jacoco'
    id 'com.bmuschko.nexus' version '2.3.1'
    id 'com.github.kt3k.coveralls' version '2.8.2'
}

group = "de.bwaldvogel"
version = '2.21'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = 'UTF-8'

ext {
    title = 'liblinear'
    description = 'Java port of Liblinear'
    url = 'https://github.com/bwaldvogel/liblinear'
}

jar {
    manifest {
        attributes 'Implementation-Title': title, 'Implementation-Version': version
    }
}

ext {
    junitVersion = '4.12'
    assertjVersion = '2.9.1'
    powermockVersion = '1.6.5'
    equalsverifierVersion = '2.5.2'
}

modifyPom {
    project {
        groupId        'de.bwaldvogel'
        artifactId     'liblinear'
        name           title
        description    'Java port of Liblinear'
        url            url
        inceptionYear '2008'

        scm {
            url 'git@github.com:bwaldvogel/liblinear.git'
            connection 'scm:git:git@github.com:bwaldvogel/liblinear.git'
            developerConnection 'scm:git:git@github.com:bwaldvogel/liblinear.git'
        }

        licenses {
            license {
                name 'The BSD License'
                url 'http://www.opensource.org/licenses/bsd-license.php'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'bwaldvogel'
                name 'Benedikt Waldvogel'
                email 'mail@bwaldvogel.de'
            }
        }
    }
}

nexus {
    sign = true
}

repositories {
    mavenCentral()
}

jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
    dependsOn test
}

wrapper {
    gradleVersion = "5.0"
    distributionType = Wrapper.DistributionType.ALL
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: junitVersion
    testCompile group: 'org.assertj', name: 'assertj-core', version: assertjVersion
    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: powermockVersion
    testCompile group: 'org.powermock', name: 'powermock-api-mockito', version: powermockVersion
    testCompile group: 'nl.jqno.equalsverifier', name: 'equalsverifier', version: equalsverifierVersion
}

// vim: set ts=4 sw=4 et:
